plugins {
    id 'com.github.node-gradle.node'
    id 'base'
}

configurations {
    web {
        canBeConsumed = true
        canBeResolved = true
    }
}

def docsInputFiles = [
    'babel.config.js',
    'docusaurus.config.js',
    'sidebars.js'
]

def docsInputDirs = [
    'blog',
    'docs',
    'src',
    'static'
]

task cleanNodeModules {
    doLast {
        delete 'node_modules'
    }
}

task docusaurusStart(type: PnpmTask) {
    args = [ 'start' ]

    inputs.file('package.json')
    inputs.file('pnpm-lock.yaml')

    for (input in docsInputFiles) {
        inputs.file(input)
    }
    for (input in docsInputDirs) {
        inputs.files(input)
    }
}

task docusaurusBuild(type: PnpmTask) {
    args = [
        'build'
    ]

    inputs.file('package.json')
        .withPropertyName("packageJson")
        .withPathSensitivity(PathSensitivity.RELATIVE)
    inputs.file('pnpm-lock.yaml')
        .withPropertyName("pnpmLock")
        .withPathSensitivity(PathSensitivity.RELATIVE)

    for (input in docsInputFiles) {
        inputs.file(input)
            .withPropertyName("docsFile-" + input)
            .withPathSensitivity(PathSensitivity.RELATIVE)
    }
    for (input in docsInputDirs) {
        inputs.dir(input)
            .withPropertyName("docsDir-" + input)
            .withPathSensitivity(PathSensitivity.RELATIVE)
    }

    outputs.cacheIf { true }
    outputs.dir(layout.buildDirectory.dir('docusaurus'))
        .withPropertyName("result")
}

artifacts {
    web(layout.buildDirectory.dir("docusaurus")) {
        builtBy docusaurusBuild
    }
}
